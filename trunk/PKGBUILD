# Maintainer: Kyle Keen <keenerd@gmail.com>

pkgname=python-jupyter_client
pkgver=8.0.1
pkgrel=1
pkgdesc="Jupyter protocol implementation and client libraries"
arch=('any')
url="https://pypi.python.org/pypi/jupyter_client"
license=('BSD')
depends=('python-ipykernel' 'python-traitlets' 'python-pyzmq' 'python-jupyter_core' 'python-dateutil' 'python-entrypoints')
makedepends=('python-build' 'python-installer' 'python-hatchling')
checkdepends=('python-pytest' 'python-pytest-jupyter' 'python-pytest-timeout' 'iproute2' 'net-tools' 'openssh')
source=("https://files.pythonhosted.org/packages/source/j/jupyter_client/jupyter_client-$pkgver.tar.gz")
sha256sums=('3f67b1c8b7687e6db09bef10ff97669932b5e6ef6f5a8ee56d444b89022c5007')

# dep cycle with python-ipykernel

build() {
  cd jupyter_client-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd jupyter_client-$pkgver
  pytest -vv
}

package() {
  cd jupyter_client-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 COPYING.md "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
